FtanML Grammar

document ::= (value | implicit-array)?

implicit-array ::= value (array | element | string | (whitespace (number | "true" | "false" | "null")))+

value ::= array | element | string | number | "true" | "false" | "null"

array ::= "[" ((value | implicit-array) ("," (value | implicit-array))*)? "]"

element ::= "<" name? pair* (value | implicit-array)?  ">"

string ::= (""" (string-content | "'" | "|")* """) | ("'" (string-content | """ | "|")* "'") | ("|" (string-content | """ | "'")* "|"?)

whitespace ::= [\p{Space}]+

number ::= "-"? (0 | ([1-9] [0-9]*)) ("." [0-9]+)? (("e" | "E") ("+" | "-")? [0-9]+)?

name ::= name-part (":" name-part)*

pair ::= name "=" value

string-content ::= ([^<>"'\|] | escaped-char | CDATA | element)

name-part ::= string | [\p{Lu}\p{Ll}\p{Lt}\p{Lm}\p{Lo}\p{Nd}\p{Nl}\p{No}_$]+

escaped-char ::= "\" ("n" | "r" | "b" | "f" | "t" | "\" | "/" | "<" | ">" | """ | "'" | ("u" [0-9A-Fa-f]+ ";"))

CDATA ::= "\[" *some_character* [^*some_character*]* *some_character* "]" (where *some_character* is one character specific to this CDATA)